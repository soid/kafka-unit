apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public"}
    }
    dependencies {
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}
apply plugin: 'release'

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'info.batey.kafka'

// scala version is customizable by gradle argument scalaVersion, e.g.: -DscalaVersion=2.10
if (System.properties.getProperty("scalaVersion") == null) {
    // the default scala version
    ext.scalaVersion = "2.11"
} else {
    ext.scalaVersion = System.properties.getProperty("scalaVersion")
    // put scala version between kafka-unit version and "-SNAPSHOT"
    String[] versionParts = project.version.tokenize("-")
    version versionParts[0] + "_" + ext.scalaVersion + (versionParts.length > 1 ? "-" + versionParts[1] : "")
}

jar {
    baseName = 'kafka-unit'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

if (!project.hasProperty("ossrhUsername")) {
    ext.ossrhUsername = "dummy"
}
if (!project.hasProperty("ossrhPassword")) {
    ext.ossrhPassword = "dummy"
}

createReleaseTag.dependsOn uploadArchives

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Kafka Unit'
                packaging 'jar'
                url 'https://github.com/chbatey/kafka-unit'
                description 'Unit testing for Kafka'
                scm {
                    connection 'scm:git@github.com:chbatey/kafka-unit.git'
                    developerConnection 'scm:git@github.com:chbatey/kafka-unit.git'
                    url 'https://github.com/chbatey/kafka-unit'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'chbatey'
                        name 'Christopher Batey'
                        email 'christopher.batey@gmail.com'
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'junit:junit:4.11'
    compile("org.apache.kafka:kafka_$scalaVersion:0.9.0.1") {
        exclude module: 'slf4j-simple'
        exclude module: 'slf4j-log4j12'
        exclude module: 'jmxtools'
        exclude module: 'jmxri'
        exclude module: 'jms'
    }
    compile('org.apache.zookeeper:zookeeper:3.4.6') {
        exclude module: 'slf4j-log4j12'
        exclude module: 'log4j'
    }

    testCompile group: 'junit', name: 'junit', version: '4.11'

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
